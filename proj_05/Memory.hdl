// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    /* if addr[14] == 0, we loadmem; if addr[14] == 1, we either loadscreen or loadkbd */
    /* in a 15-digit binary, each of the first 14-bits (up to addr[13]) are < 16384, but if addr[14] is 1 it's >= 16384 */
    DMux4Way(in=load, sel=address[13..14], a=loadmema, b=loadmemb, c=loadscreen, d=loadkbd);

    /* tried to use the same loadmem value for each of a, b in the dmux but cannot reuse inputs */
    Or(a=loadmema, b=loadmemb, out=loadmem);

    /* using the built-in version here */
    RAM16K(in=in, load=loadmem, address=address[0..13], out=memout); 
    Screen(in=in, load=loadscreen, address=address[0..12], out=screenout);
    Keyboard(out=kbdout);

    /* the converse logic from the initial dmux */
    Mux4Way16(a=memout, b=memout, c=screenout, d=kbdout, sel=address[13..14], out=out);
}
